Specification "pts".
Import "contexts".

Define pctx : olist -> olist -> prop by
 pctx nil nil;
 nabla x, pctx (pr x x::G) (cd x x::notlam x::D) := pctx G D.
Define is_pr : o -> prop by
 nabla x, is_pr (pr x x).
Define is_cd : o -> prop by
 nabla x, is_cd (cd x x).
Define is_notlam : o -> prop by
 nabla x, is_notlam (notlam x).

Theorem mem_cd : forall G D M M', pctx G D -> member (cd M M') D -> is_cd (cd M M') /\ member (notlam M) D.
induction on 2. intros. case H2. case H1. search.
case H1. case H3. apply IH to _ H5. case H6. search. search.

Theorem mem_notlam : forall G D P, pctx G D -> member (notlam P) D -> is_notlam (notlam P).
induction on 2. intros. case H2. case H1. case H1. case H3. search.
apply IH to _ H5. search.

Theorem mem_pr : forall G D E, pctx G D -> member E G -> is_pr E.
induction on 2. intros. case H2. case H1. search.
case H1. apply IH to _ H3. search.

Theorem mem_pr_cd : forall G D M M', pctx G D -> member (pr M M') G -> member (cd M M') D /\ member (notlam M) D.
induction on 2. intros. case H2. case H1. search.
case H1. apply IH to _ H3. search. 

Close sort.
Theorem pr_subst : forall G D M M' N N', nabla x, pctx G D -> {G,pr x x |- pr (M x) (M' x)} -> {G |- pr N N'}
-> {G |- pr (M N) (M' N')}.
induction on 2. intros. case H2.
apply mem_pr to _ H4. case H5. case H4. search.
search.
apply IH to _ H4 H3.
apply IH to _ H5 H3 with x = n1.
search.
apply IH to _ H4 H3.
apply IH to _ H5 H3 with x = n1.
search.
apply IH to _ H4 H3.
apply IH to _ H5 H3.
search.
apply IH to _ H4 H3.
apply IH to _ H5 H3 with x = n1.
search.
search.

Theorem pr_cd1 : forall G D M M' N, pctx G D -> {G |- pr M N} -> {D |- cd M M'} -> {G |- pr N M'}.
induction on 3. intros. case H3.
apply mem_cd to _ H4. case H5.
case H2. apply mem_pr to _ H7. case H8.
search.
case H2. apply mem_pr to _ H6. case H7.
apply IH to _ H6 H4.
apply IH to _ H7 H5.
search.
case H2. apply mem_pr to _ H6. case H7.
apply IH to _ H6 H4.
apply IH to _ H7 H5.
search.
case H2. apply mem_pr to _ H7. case H8.
apply IH to _ H7 H5.
apply IH to _ H8 H6.
search.
case H4. apply mem_notlam to _ H9. case H10.
case H2. apply mem_pr to _ H6. case H7.
case H6. apply mem_pr to _ H8. case H9.
apply IH to _ H7 H4.
apply IH to _ H9 H5.
search.
apply IH to _ H6 H4.
apply IH to _ H7 H5.
apply pr_subst to _ H9 H8.
search.
case H2. apply mem_pr to _ H4. case H5.
search.

Theorem is_lam_decide : forall G D M M', pctx G D -> {G |- pr M M'} -> {D |- notlam M} \/ exists U N, M = abs U N.
intros. case H2. apply mem_pr_cd to _ H3. search.
search. search. search. search. search. 

Theorem cd_total : forall G D M M', pctx G D -> {G |- pr M M'} -> exists N, {D |- cd M N}.
induction on 2. intros. case H2. apply mem_pr to _ H3. case H4. apply mem_pr_cd to _ H3.
search.
apply IH to _ H3. apply IH to _ H4. search.
apply IH to _ H3. apply IH to _ H4. search.
apply IH to _ H3. apply IH to _ H4.
apply is_lam_decide to _ H3. case H7.
search. case H5. apply mem_cd to _ H8. case H9.
search.
apply IH to _ H3. apply IH to _ H4. search.
search.

Define pwctx : olist -> olist -> olist -> prop by
 pwctx nil nil nil;
 nabla x, pwctx (wf x::W) (pr x x::G) (cd x x::notlam x::D) := pwctx W G D.

Define is_wf : o -> prop by
 nabla x, is_wf (wf x).

Theorem mem_wf : forall W G D M, pwctx W G D -> member M W -> is_wf M.
induction on 2. intros. case H2. case H1. search. case H1. apply IH to _ H3. search.

Theorem mem_wf_pr : forall W G D M, pwctx W G D -> member (wf M) W -> member (pr M M) G.
induction on 2. intros. case H2. case H1. search. case H1. apply IH to _ H3. search.

Theorem mem_pr_wf : forall W G D M, pwctx W G D -> member (pr M M) G -> member (wf M) W.
induction on 2. intros. case H2. case H1. search. case H1. apply IH to _ H3. search.

Theorem pr_refl : forall W G D M, pwctx W G D -> {W |- wf M} -> {G |- pr M M}.
induction on 2. intros. case H2. apply mem_wf_pr to _ H3. search.
search. apply IH to _ H3. apply IH to _ H4. search.
apply IH to _ H3. apply IH to _ H4. search.
apply IH to _ H3. apply IH to _ H4. search.

Theorem pr_includes_step : forall W G D M N, pwctx W G D -> {W |- wf M} -> {W |- step M N} -> {G |- pr M N}.
induction on 3. intros. case H3.
apply mem_wf to _ H4. case H5.
case H2. apply mem_wf to _ H5. case H6. apply IH to _ _ H4.
apply pr_refl to _ H6. search.
case H2. apply mem_wf to _ H5. case H6.
apply IH to _ H6 H4. apply pr_refl to _ H5. search.
case H2. apply mem_wf to _ H5. case H6.
apply IH to _ _ H4. apply pr_refl to _ H6. search.
case H2. apply mem_wf to _ H5. case H6. apply IH to _ _ H4. apply pr_refl to _ H5. search.
case H2. apply mem_wf to _ H7. case H8. case H7. apply mem_wf to _ H9. case H10.
apply pr_refl to _ H8. apply pr_refl to _ H10. apply pr_refl to _ H9. search.
case H2. apply mem_wf to _ H5. case H6. apply IH to _ _ H4. apply pr_refl to _ H6. search.
case H2. apply mem_wf to _ H5. case H6. apply IH to _ H6 H4. apply pr_refl to _ H5. search.

Theorem pw_implies_p : forall W G D, pwctx W G D -> pctx G D.
induction on 1. intros. case H1. search.
apply IH to H2. search.

Theorem pr_diamond : forall G D M N1 N2, pctx G D -> {G |- pr M N1} -> {G |- pr M N2} -> exists N, {G |- pr N1 N} /\ {G |- pr N2 N}.
intros.
apply cd_total to _ H2.
apply pr_cd1 to _ H3 H4.
apply pr_cd1 to _ H2 H4.
search.

Theorem cr1 : forall G D M N1 N2, pctx G D -> {G |- pr M N1} -> {G |- prn M N2} ->
 exists N, {G |- prn N1 N} /\ {G |- pr N2 N}.
induction on 3. intros. case H3.
apply mem_pr to _ H4. case H5.
search.
apply IH to _ H2 H4.
apply pr_diamond to _ H5 H7.
search.

Theorem cr2 : forall G D M N1 N2, pctx G D -> {G |- prn M N1} -> {G |- prn M N2} ->
  exists N, {G |- prn N1 N} /\ {G |- prn N2 N}.
induction on 2. intros. case H2. apply mem_pr to _ H4. case H5.
search.
apply IH to _ H4 H3.
apply cr1 to _ H5 H6.
search.

Theorem prn_concat : forall G D M N L, pctx G D -> {G |- prn M N} -> {G |- prn N L} -> {G |- prn M L}.
induction on 3. intros. case H3. apply mem_pr to _ H4. case H5.
search.
apply IH to _ H2 H4. search.

Theorem cr_eq : forall W G D M N, pwctx W G D -> {W |- wf M} -> {W |- wf N} -> {W |- eq M N} -> exists L, {G |- prn M L} /\ {G |- prn N L}.
induction on 4. intros. case H4.
apply mem_wf to _ H5. case H6.
apply pr_includes_step to _ _ H5. search.
search.
apply IH to _ _ _ H5. search.
apply IH to _ _ _ H5.
apply IH to _ _ _ H7.
apply pw_implies_p to H1.
apply cr2 to _ H9 H10.
apply prn_concat to _ H8 H13.
apply prn_concat to _ H11 H14.
search.

Theorem prn_for_preserve : forall G D U T M, pctx G D -> {G |- prn (for U T) M} -> exists V T', M = for V T'.
induction on 2. intros. case H2. apply mem_pr to _ H3. case H4.
search.
apply IH to _ H3.
case H4. apply mem_pr to _ H5. case H6. search.

Theorem prn_for_inv : forall G D U V T1 T2, pctx G D -> {G |- prn (for U T1) (for V T2)} -> {G |- prn U V} /\ nabla x, {G,pr x x |- prn (T1 x) (T2 x)}.
induction on 2. intros. case H2. apply mem_pr to _ H3. case H4.
search.
apply prn_for_preserve to _ H3.
apply IH to _ H3.
case H4. apply mem_pr to _ H7. case H8. search.

Theorem eq_includes_pr : forall W G D M N, pwctx W G D -> {G |- pr M N} -> {W |- eq M N}.
induction on 2. intros. apply pw_implies_p to H1. case H2. apply mem_pr to _ H4. case H5.  search.
apply IH to _ H4. apply IH to _ H5.
skip.

Theorem pr_implies_wf : forall W G D M N, pwctx W G D -> {G |- pr M N} -> {W |- wf N}.
induction on 2. intros. apply pw_implies_p to H1. case H2. apply mem_pr to _ H4. case H5.
apply mem_pr_wf to _ H4. search.
apply IH to _ H4. apply IH to _ H5. search.
apply IH to _ H4. apply IH to _ H5. search.
apply IH to _ H4. apply IH to _ H5. search.
apply IH to _ H4. apply IH to _ H5.
inst H7 with n1 = N'. cut H8. search.
search.

Theorem prn_implies_wf : forall W G D M N, pwctx W G D -> {G |- prn M N} -> {W |- wf N} \/ M = N.
induction on 2. intros. apply pw_implies_p to H1. case H2. apply mem_pr to _ H4. case H5.
search.
left. apply IH to _ H4. case H6. apply pr_implies_wf to _ H5. search.
apply pr_implies_wf to _ H5. search.

Theorem eq_includes_prn : forall G D W M N, pwctx W G D -> {G |- prn M N} -> {W |- eq M N}.
induction on 2. intros. case H2. apply pw_implies_p to H1. apply mem_pr to _ H3. case H5. search.
apply IH to _ H3. apply eq_includes_pr to _ H4. 
apply prn_implies_wf to _ H3. case H7. search. search.

Theorem eq_for_inv : forall W G D U V T1 T2, pwctx W G D -> {W |- wf (for U T1)} -> {W |- wf (for V T2)} -> {W |- eq (for U T1) (for V T2)} -> {W |- eq U V} /\ nabla x, {W, wf x |- eq (T1 x) (T2 x)}.
intros.
apply cr_eq to _ _ _ H4.
apply pw_implies_p to H1.
apply prn_for_preserve to _ H5.
apply prn_for_inv to _ H5.
apply prn_for_inv to _ H6.
apply eq_includes_prn to _ H8.
apply eq_includes_prn to _ H9.
apply eq_includes_prn to _ H10.
apply eq_includes_prn to _ H11.
apply prn_implies_wf to _ H9. case H16.
apply prn_implies_wf to _ H10. case H18. search.
search.
apply prn_implies_wf to _ H10. case H17. search.
search.


 
